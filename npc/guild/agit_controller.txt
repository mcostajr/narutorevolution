-	script	Agit_Event	-1,{
	end;

OnClock1900:	//Hora de início para sábado
OnClock2000:	//Hora final para sábado

OnAgitInit:
	// Verificações de tempo inicial
	if((gettime(DT_DAYOFWEEK)==SATURDAY) && (gettime(DT_HOUR)>=19 && gettime(DT_HOUR)<20)) {
		if (!agitcheck()) {
			AgitStart;
			callsub S_DisplayOwners;
		}
		end;
	}

	// Verificações de tempo final
	if ((gettime(DT_DAYOFWEEK)==SATURDAY) && (gettime(DT_HOUR)==20)) { 
		if (agitcheck()) {
			AgitEnd;
			callsub S_DisplayOwners;
		}
		end;
	}
	end;

S_DisplayOwners:
	setarray .@maps$[0],"aldeg_cas01","aldeg_cas02","aldeg_cas03","aldeg_cas04","aldeg_cas05";
	setarray .@maps$[5],"gefg_cas01","gefg_cas02","gefg_cas03","gefg_cas04","gefg_cas05";
	setarray .@maps$[10],"payg_cas01","payg_cas02","payg_cas03","payg_cas04","payg_cas05";
	setarray .@maps$[15],"prtg_cas01","prtg_cas02","prtg_cas03","prtg_cas04","prtg_cas05";
	for( set .@i, 0; .@i <= 19; set .@i, .@i+1 ) {
		if (GetCastleData(.@maps$[.@i],1)) {
			Announce "O Castelo de [" + GetCastleName(.@maps$[.@i]) + "] foi conquistado pelo Clã [" + GetGuildName(GetCastleData(.@maps$[.@i],1)) + "].",bc_all|bc_woe;
		}
		else {
			Announce "o Castelo de [" + GetCastleName(.@maps$[.@i]) + "] está atualmente desocupado.",bc_all|bc_woe;
		}
	}
	end;
}

