konoha3,181,177,5	script	Mabui#1::kafra	442,{
	mes "[Mabui]";
	mes "Olá, bem vindo.";
	mes "No que precisa de ajuda?";
	next;
	switch(select("- Curar:- Salvar:- Usar Armazem:- Cancel")) {
	case 1:
		mes "[Mabui]";
		mes "Voce ja está curado.";
		percentheal 100,100;
		close;
	case 2:
		mes "[Mabui]";
		mes "Seu ponto de retorno";
		mes "foi salvo aqui.";
		next;
		getmapxy(.@map$, .@x, .@y, 0);
		savepoint .@map$, .@x, .@y;
		mes "[Mabui]";
		mes "Muito obrigado por visitar";
		mes "a sede da Corpora?ao Kafra.";
		close2;
		break;
	case 3:
		mes "[Mabui]";
		mes "Obrigado por usar nossos serviços.";
		close2;
		openstorage;
		break;
		
	case 4:
		mes "[Mabui]";
		mes "^666666*Ufa...*^000000";
		mes "Ótimo, porque eu";
		mes "atualmente, estou descansando.";
		mes "Agora a escolha da opção";
		mes "''Cancelar'' foi um bom";
		mes "movimento vindo de sua parte.";
		close2;
		break;
	}
	end;
	
}

sunagakure,218,157,4	duplicate(kafra)	Mabui#2	442
kirigakure,215,198,4	duplicate(kafra)	Mabui#3	442
kumogakure,164,121,4	duplicate(kafra)	Mabui#4	442
iwagakure,207,146,2	duplicate(kafra)	Mabui#5	442
//treinamento,79,136,4	duplicate(kafra)	Mabui#6	442
otogakure,80,196,4	duplicate(kafra)	Mabui#7	442
kusagakure,207,87,4	duplicate(kafra)	Mabui#8	442
amegakure,215,186,4	duplicate(kafra)	Mabui#9	442
takigakure,173,151,4	duplicate(kafra)	Mabui#10	442
academia,119,24,4	duplicate(kafra)	Mabui#11	442

//===================================================
// Teleport Function
//===================================================
function	script	F_KafTele	{
	mes "[Kafra Employee]";
	mes "Please choose";
	mes "your destination.";
	next;
	.@j = select( implode(@wrpC$,":") ) - 1;
	if (@wrpC$[.@j] == "Cancel")
		return;
	// Consume "Free Ticket for Kafra Transportation" if available.
	// Do not consume if in Guild castle
	if (countitem(7060) > 0 && getarg(0) != 2)
		delitem 7060,1;
	else {
		if (Zeny < @wrpP[.@j]) {
			mes "[Kafra Employee]";
			mes "I'm sorry, but you don't have";
			mes "enough zeny for the Teleport";
			mes "Service. The fee to teleport";
			mes "to " + @wrpD$[.@j] + " is " + @wrpP[.@j] + " zeny.";
			close3;
		}
		Zeny -= @wrpP[.@j];
		RESRVPTS += (@wrpP[.@j]/16);
	}
	cutin "", 255;
	if (@wrpD$[.@j] == "Al De Baran") warp "aldebaran",168,112;
	else if (@wrpD$[.@j] == "Alberta") warp "alberta", 117, 56;
	else if (@wrpD$[.@j] == "Comodo") warp "comodo", 209, 143;
	else if (@wrpD$[.@j] == "Comodo Pharos Beacon") warp "cmd_fild07", 127, 134;
	else if (@wrpD$[.@j] == "Geffen") warp "geffen", 120, 39;
	else if (@wrpD$[.@j] == "Izlude") {
		if (checkre(RENEWAL) == true)
			warp "izlude", 128, 98;
		else
			warp "izlude", 91, 105;
	}
	else if (@wrpD$[.@j] == "Juno") warp "yuno", 158, 125;
	else if (@wrpD$[.@j] == "Mjolnir Dead Pit") warp "mjolnir_02", 99, 351;
	else if (@wrpD$[.@j] == "Morroc") warp "morocc", 156, 46;
	else if (@wrpD$[.@j] == "Orc Dungeon") warp "gef_fild10", 52, 326;
	else if (@wrpD$[.@j] == "Payon") warp "payon", 161, 58;
	else if (@wrpD$[.@j] == "Prontera") warp "prontera", 116, 72;
	else if (@wrpD$[.@j] == "Umbala") warp "umbala", 100, 154;
	end;
}

//===================================================
// Warp Points / View Points Function
//===================================================
function	script	F_KafSet	{
	deletearray @wrpC$[0], getarraysize(@wrpC$);
	deletearray @wrpD$[0], getarraysize(@wrpD$);
	deletearray @wrpP$[0], getarraysize(@wrpP$);
	deletearray @viewpX[0], getarraysize(@viewpX);
	deletearray @viewpY[0], getarraysize(@viewpY);

	.@map$ = strnpcinfo(4);
	if (.@map$ == "prontera") {
		setarray @wrpD$[0], "Izlude", "Geffen", "Payon", "Morroc", "Orc Dungeon", "Alberta";
		setarray @wrpP[0], 600, 1200, 1200, 1200, 1700, 1800;
		setarray @viewpX[0], 151, 29, 282, 152;
		setarray @viewpY[0], 29, 207, 200, 326;
	} else if (.@map$ == "alberta") {
		setarray @wrpP[0], 1200, 1800, 1800;
		setarray @wrpD$[0], "Payon", "Morroc", "Prontera";
		setarray @viewpX[0], 28, 113, 0, 0;
		setarray @viewpY[0], 229, 60, 0, 0;
	} else if (.@map$ == "aldebaran") {
		setarray @wrpP[0], 1200, 1200, 1800, 1700;
		setarray @wrpD$[0], "Geffen", "Juno" , "Izlude", "Mjolnir Dead Pit";
	} else if (.@map$ == "comodo" ) {
		setarray @wrpD$[0], "Morroc", "Comodo Pharos Beacon", "Umbala";
		setarray @wrpP[0], 1800, 1200, 1800;
	} else if (.@map$ == "cmd_fild07") {
		setarray @wrpD$[0], "Comodo", "Morroc";
		setarray @wrpP[0], 1200, 1200;
	} else if (.@map$ == "geffen") {
		setarray @wrpD$[0], "Prontera", "Al De Baran", "Orc Dungeon", "Mjolnir Dead Pit";
		setarray @wrpP[0], 1200, 1200, 1700, 1700;
		setarray @viewpX[0], 120, 203;
		setarray @viewpY[0], 62, 123;
	} else if (.@map$ == "izlude") {
		setarray @wrpD$[0], "Geffen", "Payon", "Morroc", "Al De Baran";
		setarray @wrpP[0], 1200, 1200, 1200, 1800;
	} else if (.@map$ == "morocc") {
		setarray @wrpD$[0], "Prontera", "Payon", "Alberta", "Comodo", "Comodo Pharos Beacon";
		setarray @wrpP[0], 1200, 1200, 1800, 1800, 1200;
		setarray @viewpX[0], 156, 163, 28, 292;
		setarray @viewpY[0], 97, 260, 167, 211;
	} else if (.@map$ == "umbala") {
		setarray @wrpD$[0], "Comodo";
		setarray @wrpP[0], 1800;
	} else if (.@map$ == "payon") {
		setarray @wrpD$[0], "Prontera", "Alberta", "Morroc";
		setarray @wrpP[0], 1200, 1200, 1200;
	} else if (.@map$ == "yuno") {
		setarray @wrpD$[0], "Al De Baran";
		setarray @wrpP[0], 1200;
		setarray @viewpX[0], 328, 278, 153, 0;
		setarray @viewpY[0], 108, 221, 187, 0;
	} else if (.@map$ == "job3_rune01") {
		setarray @wrpD$[0], "Izlude", "Geffen", "Payon", "Morroc", "Alberta";
		setarray @wrpP[0], 600, 1200, 1200, 1200, 1800;
	}

	.@warp_size = getarraysize(@wrpD$);

	// VIP pricing varies across the regional servers; this is most practical.
	if (VIP_SCRIPT && !vip_status(VIP_STATUS_ACTIVE)) {
		for ( .@i = 0; .@i < .@warp_size; .@i++ )
			@wrpP[.@i] *= 2;
	}

	for ( .@i = 0; .@i < .@warp_size; .@i++ )
		@wrpC$[.@i] = @wrpD$[.@i] + " -> " + @wrpP[.@i] + " z";
	@wrpC$[ .@warp_size ] = "Cancel";
	return;
}
